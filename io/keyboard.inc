;----------------------------------------------------------------------------------------------
; MSX Emulator for X680x0 - emes.x
;
;    Copyright 1997-1998 nir
;



	.xdef KeyInterrupt,_Org_KeyInterrupt
	.xdef _noKeyInt
	.xdef _keyboard_LED



	SET_OFFSET_IO	keyboard_LED



NOKEY		.equ	0
RELEASE_KEY	.equ	$53	* [登録]
DEBUGGER_KEY	.equ	$62	* [COPY]



*----------------------------------------------------------------------------------------------
* キーボード割り込み
KeyInterrupt:
	movem.l	d0-d1/a0,-(sp)
	
	
	*-- キーボードからのデータ取得
	moveq.l	#0,d0
	move.b	$e8802f,d0		* MFP USART データレジスタ
	
	
	*-- IMRA : バッファフル割り込み禁止
	bclr.b	#4,$e88013		* 割り込みマスクレジスタ A
	
	
	*-- 割り込みレベルを割り込み発生前の状態に設定
	move.w	12(sp),d1		* 割り込み発生時の SR 取得
	or.w	#$2000,d1		* Supervisor State : 1 = Supervisor
	move.w	d1,sr			* 割り込み時の SR に設定
	
	
	*----------------------
	* キーボード処理切り放しキーが押された場合
	cmpi.b	#RELEASE_KEY,d0				* 
	beq.s	releaseKeyInterrupt			* 10/ 8 (T/F)
	
	*----------------------
	* Z80デバッガ起動キーが押された場合
	cmpi.b	#DEBUGGER_KEY,d0			* 
	beq.s	debugger_on				* 10/ 8 (T/F)
	
	
	*----------------------
	lea.l	_MSXKeyTable(pc),a0			*  8
	
	moveq.l	#0,d1					*  4
	
	add.b	d0,d0					*  4
	bcs.s	1f					* 10/ 8 (T/F)
	
	*-- Key ON
	move.b	KeyBitTable(pc,d0.w),d1			* 14
	move.b	KeyBitTable+1(pc,d0.w),d0		* 14
	not.b	d0					*  4
	and.b	d0,(a0,d1.w)				* 18
	
	bra.s	2f					* 10
	
1:	*-- Key OFF
	move.b	KeyBitTable(pc,d0.w),d1			* 14
	move.b	KeyBitTable+1(pc,d0.w),d0		* 14
	or.b	d0,(a0,d1.w)				* 18
	
2:
	
	
ret_keyint:
	
	*-- IMRA : バッファフル割り込み許可
	bset.b	#4,$e88013		* 割り込みマスクレジスタ A
	
	
	movem.l	(sp)+,d0-d1/a0
	rte

releaseKeyInterrupt:
	move.l	(_Org_KeyInterrupt),(VECTOR_KEYINT*4)
	
	move.b	#-1,(_noKeyInt)
	clr.b	(changeKeyStatus)
	
	bra.s	ret_keyint

debugger_on:
	trap	#12
	
	bra.s	ret_keyint



*----------------------------------------------------------------------------------------------
KeyBitTable:	* group,bit
		
		* 00		01 [ESC]	02 [1]		03 [2]
	.dc.b	NOKEY,NOKEY,	 7,%0000_0100,	 0,%0000_0010,	 0,%0000_0100
	
		* 04 [3]	05 [4]		06 [5]		07 [6]
	.dc.b	 0,%0000_1000,	 0,%0001_0000,	 0,%0010_0000,	 0,%0100_0000
	
		* 08 [7]	09 [8]		0A [9]		0B [0]
	.dc.b	 0,%1000_0000,	 1,%0000_0001,	 1,%0000_0010,	 0,%0000_0001
	
		* 0C [-]	0D [^]		0E [\]		0F [BS]
	.dc.b	 1,%0000_0100,	 1,%0000_1000,	 1,%0001_0000,	 7,%0010_0000
	
		* 10 [TAB]	11 [Q]		12 [W]		13 [E]
	.dc.b	 7,%0000_1000,	 4,%0100_0000,	 5,%0001_0000,	 3,%0000_0100
	
		* 14 [R]	15 [T]		16 [Y]		17 [U]
	.dc.b	 4,%1000_0000,	 5,%0000_0010,	 5,%0100_0000,	 5,%0000_0100
	
		* 18 [I]	19 [O]		1A [P]		1B [@]
	.dc.b	 3,%0100_0000,	 4,%0001_0000,	 4,%0010_0000,	 1,%0010_0000
	
		* 1C [[]	1D [RETURN]	1E [A]		1F [S]
	.dc.b	 1,%0100_0000,	 7,%1000_0000,	 2,%0100_0000,	 5,%0000_0001
	
		* 20 [D]	21 [F]		22 [G]		23 [H]
	.dc.b	 3,%0000_0010,	 3,%0000_1000,	 3,%0001_0000,	 3,%0010_0000
	
		* 24 [J]	25 [K]		26 [L]		27 [;]
	.dc.b	 3,%1000_0000,	 4,%0000_0001,	 4,%0000_0010,	 1,%1000_0000
	
		* 28 [:]	29 []]		2A [Z]		2B [X]
	.dc.b	 2,%0000_0001,	 2,%0000_0010,	 5,%1000_0000,	 5,%0010_0000
	
		* 2C [C]	2D [V]		2E [B]		2F [N]
	.dc.b	 3,%0000_0001,	 5,%0000_1000,	 2,%1000_0000,	 4,%0000_1000
	
		* 30 [M]	31 [,]		32 [.]		33 [/]
	.dc.b	 4,%0000_0100,	 2,%0000_0100,	 2,%0000_1000,	 2,%0001_0000
	
		* 34 [_]	35 [SPACE]	36 [HOME]	37 [DEL]
	.dc.b	 2,%0010_0000,	 8,%0000_0001,	 8,%0000_0010,	 8,%0000_1000
	
		* 38 [ROLL UP]	39 [ROLL DOWN]	3A [UNDO]	3B [←]
	.dc.b	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY,	 8,%0001_0000
	
		* 3C [↑]	3D [→]		3E [↓]		3F [CLR]
	.dc.b	 8,%0010_0000,	 8,%1000_0000,	 8,%0100_0000,	 8,%0000_0010
	
		* 40 [/] ten	41 [*] ten	42 [-] ten	43 [7] ten
	.dc.b	 9,%0000_0100,	 9,%0000_0001,	10,%0010_0000,	10,%0000_0100
	
		* 44 [8] ten	45 [9] ten	46 [+] ten	47 [4] ten
	.dc.b	10,%0000_1000,	10,%0001_0000,	 9,%0000_0010,	 9,%1000_0000
	
		* 48 [5] ten	49 [6] ten	4A [=] ten	4B [1] ten
	.dc.b	10,%0000_0001,	10,%0000_0010,	NOKEY,NOKEY,	 9,%0001_0000
	
		* 4C [2] ten	4D [3] ten	4E [ENTER] ten	4F [0] ten
	.dc.b	 9,%0010_0000,	 9,%0100_0000,	 7,%1000_0000,	 9,%0000_1000
	
		* 50 [,] ten	51 [.] ten	52 [記号入力]	53 [登録]
	.dc.b	10,%0100_0000,	10,%1000_0000,	NOKEY,NOKEY,	NOKEY,NOKEY
	
		* 54 [HELP]	55 [XF1]	56 [XF2]	57 [XF3]
	.dc.b	NOKEY,NOKEY,	 6,%0000_0100,	NOKEY,NOKEY,	NOKEY,NOKEY
	
		* 58 [XF4]	59 [XF5]	5A [かな]	5B [ローマ字]
	.dc.b	NOKEY,NOKEY,	 7,%0100_0000,	 6,%0001_0000,	NOKEY,NOKEY
	
		* 5C [ｺｰﾄﾞ入力]	5D [CAPS]	5E [INS]	5F [ひらがな]
	.dc.b	NOKEY,NOKEY,	 6,%0000_1000,	 8,%0000_0100,	NOKEY,NOKEY
	
		* 60 [全角]	61 [BREAK]	62 [COPY]	63 [F1]
	.dc.b	NOKEY,NOKEY,	 7,%0001_0000,	NOKEY,NOKEY,	 6,%0010_0000
	
		* 64 [F2]	65 [F3]		66 [F4]		67 [F5]
	.dc.b	 6,%0100_0000,	 6,%1000_0000,	 7,%0000_0001,	 7,%0000_0010
	
		* 68 [F6]	69 [F7]		6A [F8]		6B [F9]
	.dc.b	 6,%0010_0000,	 6,%0100_0000,	 6,%1000_0000,	 7,%0000_0001
	
		* 6C [F10]	6D 		6E 		6F 
	.dc.b	 7,%0000_0010,	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY
	
		* 70 [SHIFT]	71 [CTRL]	72 [OPT.1]	73 [OPT.2]
	.dc.b	 6,%0000_0001,	 6,%0000_0010,	NOKEY,NOKEY,	NOKEY,NOKEY
	
		* 74 		75 		76 		77 
	.dc.b	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY
	
		* 78 		79 		7A 		7B 
	.dc.b	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY
	
		* 7C 		7D 		7E 		7F 
	.dc.b	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY,	NOKEY,NOKEY




*----------------------------------------------------------------------------------------------
_Debug_KeySense:
	;--
	movem.l	d3/d4/a2,-(sp)				;
	
	;-- Human68k ｷｰ状態ﾃｰﾌﾞﾙ
	lea	HumanKeyTable,a0
	
	;-- Human68k -> MSX ｷｰ変換ﾃｰﾌﾞﾙ
	lea	keyConvTable(pc),a1			*  8
	
	;-- MSX ｷｰ状態ﾃｰﾌﾞﾙ
	lea	_MSXKeyTable(pc),a2			*  8
	
	;-- MSX ｷｰ状態ﾃｰﾌﾞﾙｸﾘｱ
	move.l	#-1,(a2)
	move.l	#-1,4(a2)
	move.l	#-1,8(a2)
	move.l	#-1,12(a2)
	
	
	moveq.l	#0,d3					*  4
	moveq.l	#0,d4
	
	move.w	#16/2-1,d2				*  8	ｷｰｺｰﾄﾞｸﾞﾙｰﾌﾟ
1:
	move.w	(a0)+,d0				*  8
	moveq.l	#16-1,d1				*  8
2:
	add.w	d0,d0					*  4
	bcc.s	3f					* 10/ 8 (T/F)
	
	*-- キーが押されている
	move.b	(a1)+,d3				*  8	MSX キーグループ
	move.b	(a1)+,d4				*  8	MSX ビット位置
	not.b	d4					*  4
	and.b	d4,(a2,d3.w)				* 
	
	bra	4f					* 10
	
	*-- キーが押されていない
3:	addq.w	#2,a1					*  8
	
4:	dbra	d1,2b					* 10/14 (T/F)
	
	dbra	d2,1b					* 10/14 (T/F)
	
	
	*-- キーボード割り込み切り替えキーのチェック
	* 切り替えキーが押されていない場合はチェック終了
	tst.b	15(a2)					* 12
	bne.s	@f					* 10/ 8 (T/F)
	
	* 切り替えキーが押された瞬間 ?
	tst.b	16(a2)					* 12
	beq.s	@f					* 10/ 8 (T/F)
	
	* 切り替えキー OFF->ON なので切り替え
	sf.b	(_noKeyInt)				* 
	
	pea.l	KeyInterrupt				* キー入力処理
	move.w	#VECTOR_KEYINT,-(sp)			* 
	.dc.w	$ff25					* DOS _INTVCS
	addq.w	#6,sp
	
	;-- デバッグモード表示LED消灯
	bset.b	#2,(_keyboard_LED)			;
	
	move.l	d0,(_Org_KeyInterrupt)
@@:	
	* 切り替えキーの状態保存
	move.b	15(a2),16(a2)				* 
	
	movem.l	(sp)+,d3/d4/a2				* 
	
	rts



keyConvTable:
* GROUP 0
	*	[6]		[5]		[4]		[3]
	.dc.b	 0,%0100_0000,	 0,%0010_0000,	 0,%0001_0000,	 0,%0000_1000
	*	[2]		[1]		[ESC]		なし
	.dc.b	 0,%0000_0100,	 0,%0000_0010,	 7,%0000_0100,	 0,0
	
* GROUP 1
	*	[BS]		[\]		[^]		[-]
	.dc.b	 7,%0010_0000,	 1,%0001_0000,	 1,%0000_1000,	 1,%0000_0100
	*	[0]		[9]		[8]		[7]
	.dc.b	 0,%0000_0001,	 1,%0000_0010,	 1,%0000_0001,	 0,%1000_0000
	
* GROUP 2
	*	[U]		[Y]		[T]		[R]
	.dc.b	 5,%0000_0100,	 5,%0100_0000,	 5,%0000_0010,	 4,%1000_0000
	*	[E]		[W]		[Q]		[TAB]
	.dc.b	 3,%0000_0100,	 5,%0001_0000,	 4,%0100_0000,	 7,%0000_1000
	
* GROUP 3
	*	[S]		[A]		[CR]		[[]
	.dc.b	 5,%0000_0001,	 2,%0100_0000,	 7,%1000_0000,	 1,%0100_0000
	*	[@]		[P]		[O]		[I]
	.dc.b	 1,%0010_0000,	 4,%0010_0000,	 4,%0001_0000,	 3,%0100_0000
	
* GROUP 4
	*	[;]		[L]		[K]		[J]
	.dc.b	 1,%1000_0000,	 4,%0000_0010,	 4,%0000_0001,	 3,%1000_0000
	*	[H]		[G]		[F]		[D]
	.dc.b	 3,%0010_0000,	 3,%0001_0000,	 3,%0000_1000,	 3,%0000_0010
	
* GROUP 5
	*	[N]		[B]		[V]		[C]
	.dc.b	 4,%0000_1000,	 2,%1000_0000,	 5,%0000_1000,	 3,%0000_0001
	*	[X]		[Z]		[]]		[:]
	.dc.b	 5,%0010_0000,	 5,%1000_0000,	 2,%0000_0010,	 2,%0000_0001
	
* GROUP 6
	*	[DEL]		[HOME]		[SPACE]		[_]
	.dc.b	 8,%0000_1000,	 8,%0000_0010,	 8,%0000_0001,	 2,%0010_0000
	*	[/]		[>]		[<]		[M]
	.dc.b	 2,%0001_0000,	 2,%0000_1000,	 2,%0000_0100,	 4,%0000_0100
	
* GROUP 7
	*	[CLR]		[↓]		[→]		[↑]
	.dc.b	 8,%0000_0010,	 8,%0100_0000,	 8,%1000_0000,	 8,%0010_0000
	*	[←]		[UNDO]		[ROLL DOWN]	[ROLL UP]
	.dc.b	 8,%0001_0000,	 0,0,		 0,0,		 0,0
	
* GROUP 8
	*	[4] TEN		[+] TEN		[9] TEN		[8] TEN
	.dc.b	 9,%1000_0000,	 9,%0000_0010,	10,%0001_0000,	10,%0000_1000
	*	[7] TEN		[-] TEN		[*] TEN		[/] TEN
	.dc.b	10,%0000_0100,	10,%0010_0000,	 9,%0000_0001,	 9,%0000_0100
	
* GROUP 9
	*	[0] TEN		[ENTER] TEN	[3] TEN		[2] TEN
	.dc.b	 9,%0000_1000,	 7,%1000_0000,	 9,%0100_0000,	 9,%0010_0000
	*	[1] TEN		[=] TEN		[6] TEN		[5] TEN
	.dc.b	 9,%0001_0000,	 0,0,		10,%0000_0010,	10,%0000_0001
	
* GROUP A
	*	[XF3]		[XF2]		[XF1]		[HELP]
	.dc.b	 0,0,		 0,0,		 6,%0000_0100,	 0,0
	*	[登録]		[記号]		[.] TEN		[,] TEN
	.dc.b	15,%1111_1111,	 0,0,		10,%1000_0000,	10,%0100_0000
	
* GROUP B
	*	[ひらがな]	[INS]		[CAPS]		[ｺｰﾄﾞ入力]
	.dc.b	 0,0,		 8,%0000_0100,	 6,%0000_1000,	 0,0
	*	[ローマ字]	[かな]		[XF5]		[XF4]
	.dc.b	 0,0,		 6,%0001_0000,	 7,%0100_0000,	 0,0
	
* GROUP C
	*	[F5]		[F4]		[F3]		[F2]
	.dc.b	 7,%0000_0010,	 7,%0000_0001,	 6,%1000_0000,	 6,%0100_0000
	*	[F1]		[COPY]		[BREAK]		[全角]
	.dc.b	 6,%0010_0000,	 0,0,		 7,%0001_0000,	 0,0
	
* GROUP D
	*	なし		なし		なし		[F10]
	.dc.b	 0,0,		 0,0,		 0,0,		 7,%0000_0010
	*	[F9]		[F8]		[F7]		[F6]
	.dc.b	 7,%0000_0001,	 6,%1000_0000,	 6,%0100_0000,	 6,%0010_0000
	
* GROUP E
	*	なし		なし		なし		なし
	.dc.b	 0,0,		 0,0,		 0,0,		 0,0
	*	[OPT.2]		[OPT.1]		[CTRL]		[SHIFT]
	.dc.b	 0,0,		 0,0,		 6,%0000_0010,	 6,%0000_0001

* GROUP F	(ワード単位で処理するためのダミー)
	*	なし				なし		なし
	.dc.b	 0,0,		 0,0,		 0,0,		 0,0
	*	なし				なし		なし
	.dc.b	 0,0,		 0,0,		 0,0,		 0,0



*----------------------------------------------------------------------------------------------



; Y0
	;[7]	[6]	[5]	[4]	[3]	[2]	[1]	[0]
; Y1
	;[;]	[[]	[@]	[\]	[^]	[-]	[9]	[8]
; Y2
	;[B]	[A]	[_]	[/]	[.]	[,]	[]]	[:]
; Y3
	;[J]	[I]	[H]	[G]	[F]	[E]	[D]	[C]
; Y4
	;[R]	[Q]	[P]	[O]	[N]	[M]	[L]	[K]
; Y5
	;[Z]	[Y]	[X]	[W]	[V]	[U]	[T]	[S]
; Y6
	;[F3]	[F2]	[F1]	[かな]	[CAPS]	[GRAPH]	[CTRL]	[SHIFT]
; Y7
	;[RET]	[SEL]	[BS]	[STOP]	[TAB]	[ESC]	[F5]	[F4]
; Y8
	;[→]	[↓]	[↑]	[←]	[DEL]	[INS]	[CLS]	[SPACE]
; Y9
	;[4]	[3]	[2]	[1]	[0]	[/]	[+]	[*]
; Y10
	;[.]	[,]	[-]	[9]	[8]	[7]	[6]	[5]


*----------------------------------------------------------------------------------------------
_MSXKeyTable:		.dcb.b	16,-1
changeKeyStatus:	.dc.b	-1

	.even
_keyboard_LED:		.dc.b	$ff
_now_keyboard_LED:	.dc.b	$80

	.even
_Org_KeyInterrupt:	.dc.l	0

_noKeyInt:		.dc.w	0	* デバッグ用 : キーボード割り込みを登録しない
